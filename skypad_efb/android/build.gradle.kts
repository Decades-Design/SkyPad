val localProperties = java.util.Properties().also { props ->
    file("local.properties").takeIf { it.exists() }?.inputStream()?.use { props.load(it) }
}

val mapboxDownloadsToken: String =
    localProperties.getProperty("MAPBOX_DOWNLOADS_TOKEN")
        ?: providers.gradleProperty("MAPBOX_DOWNLOADS_TOKEN").orElse("").get()

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url = uri("https://api.mapbox.com/downloads/v2/releases/maven")
            authentication {
                create<BasicAuthentication>("basic")
            }
            credentials {
                username = "mapbox"
                password = mapboxDownloadsToken
            }
        }
    }
}

val newBuildDir: Directory =
    rootProject.layout.buildDirectory
        .dir("../../build")
        .get()
rootProject.layout.buildDirectory.value(newBuildDir)

subprojects {
    val newSubprojectBuildDir: Directory = newBuildDir.dir(project.name)
    project.layout.buildDirectory.value(newSubprojectBuildDir)
}
subprojects {
    project.evaluationDependsOn(":app")
}

tasks.register<Delete>("clean") {
    delete(rootProject.layout.buildDirectory)
}
